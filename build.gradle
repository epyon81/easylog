plugins {
    id "com.jfrog.bintray" version "1.7.2"
    id 'pl.allegro.tech.build.axion-release' version '1.4.1'
    id 'com.github.kt3k.coveralls' version '2.7.0'
}

apply plugin: 'distribution'
apply plugin: 'jacoco'

def easylogGithubRepo = 'epyon81/easylog'
def easylogWebsiteUrl = "https://github.com/$easylogGithubRepo"
def easylogScmUrl = "https://github.com/epyon81/${easylogGithubRepo}.git"
def easylogLicenseUrl = "https://github.com/$easylogGithubRepo/blob/master/LICENSE"
def easylogIssuesUrl = "https://github.com/$easylogGithubRepo/issues"

def pomConfig = {
    url easylogWebsiteUrl

    scm {
        url easylogScmUrl
    }

    licenses {
        license {
            name 'MIT'
            url easylogLicenseUrl
            distribution 'repo'
        }
    }
}

scmVersion {
    hooks {
        pre 'fileUpdate', [file: 'README.md', pattern: { v, c -> /<version>$v<\/version>/ }, replacement: { v, c -> "<version>$v</version>" }]
        pre 'fileUpdate', [file: 'README.md', pattern: { v, c -> /:$v'/ }, replacement: { v, c -> ":$v'" }]
        pre 'commit'
    }
}

allprojects {
    project.version = scmVersion.version

    repositories {
        jcenter()
        maven { url 'http://repository.jboss.org/nexus/content/groups/public' }
        maven { url 'http://repository.jboss.org/nexus/content/repositories/deprecated' }
        maven { url 'http://repo.jfrog.org/artifactory/repo' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'jacoco'

    group 'com.github.epyon81.easylog'

    ext {
        junitVersion = '4.12'
        byteBuddyVersion = '1.3.10'
        juelVersion = '2.2.7'
        assertjVersion = '3.5.2'
        mockitoVersion = '2.1.0-beta.120'
        springVersion = '4.2.5.RELEASE'
        aspectjVersion = '1.8.9'
        slf4jVersion = '1.7.21'
        guiceVersion = '4.0'
        javaeeVersion = '7.0'
        reflectionsVersion = '0.9.10'
    }

    sourceCompatibility = JavaVersion.VERSION_1_8

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar, javadocJar
    }

    dependencies {
        testCompile "junit:junit:$junitVersion"
        testCompile "org.assertj:assertj-core:$assertjVersion"
    }

    tasks.withType(Test) {
        jacoco {
            append = true
            classDumpFile = file("${rootDir}/build/jacoco/classpathdumps")
            destinationFile = file("${rootDir}/build/jacoco/jacocoTest.exec")
        }
    }

    jacoco {
        toolVersion = '0.7.8-SNAPSHOT'
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            html.enabled = true
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourcesJar
                artifact javadocJar

                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', project.description)
                    root.children().last() + pomConfig
                }
            }
        }
    }

    bintray {
        user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')

        publications = ['mavenJava']
        publish = true

        pkg {
            githubRepo = easylogGithubRepo
            repo = 'maven'
            name = project.name
            websiteUrl = easylogWebsiteUrl
            issueTrackerUrl = easylogIssuesUrl
            vcsUrl = easylogScmUrl
            licenses = ['MIT']
            publicDownloadNumbers = true

            version {
                vcsTag = "v$project.version"
            }
        }
    }

    project.afterEvaluate {
        bintray.pkg.desc = project.description
    }
}

distributions {
    main {
        contents {
            subprojects.each { sp ->
                into(sp.name) {
                    into('lib') {
                        from sp.configurations.runtime
                    }
                    from sp.jar
                }
            }
        }
    }
}

task jacocoRootReport(type: JacocoReport, group: 'Coverage reports') {
    description = 'Generates an aggregate report from all subprojects'
    dependsOn(subprojects.test)

    additionalSourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(subprojects.sourceSets.main.output)
    executionData = files(subprojects.jacocoTestReport.executionData)

    reports {
        html.enabled = true // human readable
        xml.enabled = true // required by coveralls
    }

    doFirst {
        executionData = files(executionData.findAll { it.exists() })
    }
}

coveralls {
    sourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten()
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}

tasks.coveralls {
    group = 'Coverage reports'
    description = 'Uploads the aggregated coverage report to Coveralls'

    dependsOn jacocoRootReport
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}
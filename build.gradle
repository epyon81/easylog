subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    group 'com.github.epyon81.easylog'

    ext {
        junitVersion = '4.12'
        byteBuddyVersion = '1.3.10'
        juelVersion = '2.2.7'
        assertjVersion = '3.4.1'
        mockitoVersion = '1.+'
        springVersion = '4.2.5.RELEASE'
        aspectjVersion = '1.8.9'
        slf4jVersion = '1.7.21'
        guiceVersion = '4.0'
        javaeeVersion = '7.0'
    }

    sourceCompatibility = 1.8

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    repositories {
        mavenCentral()
        maven { url 'http://repository.jboss.org/nexus/content/groups/public' }
        maven { url 'http://repository.jboss.org/nexus/content/repositories/deprecated' }
        maven { url 'http://repo.jfrog.org/artifactory/repo' }
    }

    dependencies {
        testCompile "junit:junit:$junitVersion"
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourceJar
                artifact javadocJar
            }
        }
    }
}
